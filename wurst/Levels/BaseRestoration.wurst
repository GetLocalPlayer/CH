package BaseRestoration
import ClosureForGroups
import Base
import ChatCommand
import TestMode


constant COMMAND_OFF = "DisableBaseRestoration"
constant SHORT_COMMAND_OFF = "dbr"
constant COMMAND_ON = "EnableBaseRestoration"
constant SHORT_COMMAND_ON = "ebr"
constant RESTORATION_INTERVAL = 0.2


function heal()
	forUnitsInRect(baseRect) (unit u) ->
		if u.isType(UNIT_TYPE_HERO) and u.isAlive()
			u.setLife(u.getMaxHP())
			u.setMana(u.getMaxMana())

init
	let t = CreateTimer()
	..startPeriodic(RESTORATION_INTERVAL, function heal)		
	
	TestMode.onEnable() -> 
		var cb = ChatCommand.register(COMMAND_OFF) (string subcommand) ->
			t.pause()
			print("Restoration at the base has been disabled")

		ChatCommand.register(SHORT_COMMAND_OFF, cb)

		cb = ChatCommand.register(COMMAND_ON) subcommand ->
			t.startPeriodic(RESTORATION_INTERVAL, function heal)
			print("Restoration at the base has been enabled")
			
		ChatCommand.register(SHORT_COMMAND_ON, cb)