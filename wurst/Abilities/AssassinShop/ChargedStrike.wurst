package ChargedStrike
import Assets
import AbilityTooltipGenerator
import AssassinShop
import AttackModifier

import LinkedListModule
import LinkedList
import ClosureForGroups
import ClosureTimers
import LigthingEx


public abstract class ChargedStrike
	static constant int SHOP_COST = 1
	static constant int LEVELS = 5
	static constant ICON = Icons.pASBTNStormHammer
	static constant NAME = "Charged Strike"
	static constant DESCRIPTION = "Gives a chance to hurl a lightning on each hit that bounces to nearby enemies.|n|n[|cffff0000Attack Modifier|r]"
	static constant int CHANCE = 40
	static constant int NUMBER_OF_TARGETS = 6
	static constant real array DAMAGE = [50., 70., 90., 110., 130.]
	static constant real BOUNCE_RANGE = 450.
	static constant real LIFETIME = 1.
	static constant real BOUNCE_DELAY = 0.5

	static constant LIGHT_TYPE = LIGHTNING_CHAIN_LIGHTNING_SEECONDARY
	static constant LIGHT_OFFSET = vec3(0, 0, 75)

	static constant FX_TARGET = Abilities.boltImpact
	static constant FX_ATTACH_TARGET = "origin"

	static constant ID = compiletime(ABIL_ID_GEN.next())

	/** Units that will be taken in account on bouncing, ignoring the targets allowed 
	condition. Basically, used only in the passive effect of "Lightning Rod" item. */
	private static let targetsAllowed = CreateGroup()

	static abstract class BounceCallback
		use LinkedListModule

		protected abstract function run(unit target)

	static function addTargetsAllowed(vararg unit units)
		for u in units
			targetsAllowed.add(u)

	static function removeTargetsAllowed(vararg unit units)
		for u in units
			targetsAllowed.remove(u)

	static function onBounce(BounceCallback cb) returns BounceCallback
		return cb

	@compiletime protected static function build()
		let tooltipGen = new AbilityTooltipGenerator(NAME, DESCRIPTION)
		new AttackModifier(ID, LEVELS, tooltipGen)
		..setIconNormal(ICON)
		..setArtTarget("")
		..setHeroAbility(true)
		..setRequirements("")
		..setRace(Race.Human)
		..setName(NAME)
		..registerTooltipGenerator(tooltipGen)
		..tooltipStartListen()
		..addTooltipProperty("Chance", (int lvl) -> CHANCE.toString())
		..addTooltipProperty("Damage", (int lvl) -> DAMAGE[lvl - 1].toInt())
		..addTooltipProperty("Number of Targets", (int lvl) -> NUMBER_OF_TARGETS)
		..presetAreaofEffect((lvl) -> BOUNCE_RANGE)
		..tooltipStopListen()
		assassinShopDef.addAbilitySold(ID, true, ICON, NAME, tooltipGen.getTooltipLearnExtended(), SHOP_COST)

	protected static function onInit()
		Preload(FX_TARGET)

		AttackModifier.onHit(ID) (unit caster, unit target, int level) ->
			if GetRandomInt(1, 100) <= CHANCE
				apply(caster, target, DAMAGE[level - 1])
			return true
			
	static function apply(unit caster, unit target, real damage)
		let affected = asList<unit>(target)
		caster.damageTarget(target, damage, false, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_LIGHTNING, WEAPON_TYPE_WHOKNOWS)
		new LigthingEx(LIGHT_TYPE, caster, target, LIGHT_OFFSET, LIGHT_OFFSET)
		..setLifetime(LIFETIME)
		..setFadepoint(LIFETIME)
		flashEffect(FX_TARGET, target, FX_ATTACH_TARGET)
		doPeriodicallyCounted(BOUNCE_DELAY, NUMBER_OF_TARGETS - 1) (CallbackCounted cb) ->
			let prevTarget = affected.peek()			
			let enumGroup = CreateGroup()
			forUnitsInRange(prevTarget.getPos(), BOUNCE_RANGE, true) (unit u) ->
				if u.isAlive() and not u.isType(UNIT_TYPE_MAGIC_IMMUNE) and not affected.has(u)
					if u.isEnemyOf(caster) or targetsAllowed.has(u)
						enumGroup.add(u)
			let randomUnit = enumGroup.getRandom()
			enumGroup.destr()
			if randomUnit != null
				caster.damageTarget(randomUnit, damage, false, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_LIGHTNING, WEAPON_TYPE_WHOKNOWS)
				new LigthingEx(LIGHT_TYPE, prevTarget, randomUnit, LIGHT_OFFSET, LIGHT_OFFSET)
				..setLifetime(LIFETIME)
				..setFadepoint(LIFETIME)
				flashEffect(FX_TARGET, randomUnit, FX_ATTACH_TARGET)
				affected.add(randomUnit)
				for cb in BounceCallback
					cb.run(randomUnit)
			else
				destroy cb
			
init
	ChargedStrike.onInit()
