package CorruptedTouch
import OrderIds
import ObjectIds
import ObjectIdGenerator
import BuffObjEditing
import AbilityObjEditing
import AbilityTooltipGenerator
import CurseMasterShop
import AttackModifier

import DummyCaster


public class CorruptedTouch
	static constant int SHOP_COST = 1
	static constant int LEVELS = 5
	static constant ICON = "ReplaceableTextures\\CommandButtons\\PASBTNCorruptedTouch.blp"
	static constant NAME = "Corrupted Touch"
	static constant DESC = "The hero's attacks reduce a target's armor on hit.|n|n[|cffff0000Attack Modifier|r]"
	static constant BUFF_DESC = "This unit's armor is redused."
	static constant TARGETS_ALLOWED = "air,ground,enemy"
	static constant real DURATION = 10
	static constant int array ARMOR_REDUCTION = [3, 4, 5, 6, 7]

	static constant ID = compiletime(ABIL_ID_GEN.next())
	static constant BUFF_ID = compiletime(BUFF_ID_GEN.next())
	static constant DUMMY_ABIL_ID = compiletime(ABIL_ID_GEN.next())

	@compiletime
	private static function build()
		let tooltipGen = new AbilityTooltipGenerator(NAME, DESC)
		new AttackModifier(ID, LEVELS, tooltipGen)
		..setIconNormal(ICON)
		..setArtTarget("")
		..setHeroAbility(true)
		..setRequirements("")
		..setRace(Race.Human)
		..setName(NAME)
		..registerTooltipGenerator(tooltipGen)
		..tooltipStartListen()
		..addTooltipProperty("Armor Reduction", (int lvl) -> ARMOR_REDUCTION[lvl - 1])
		..addTooltipProperty("Duration", (int lvl) -> DURATION)
		..tooltipStopListen()
		curseMasterShopDef.addAbilitySold(ID, true, ICON, NAME, tooltipGen.getTooltipLearnExtended(), SHOP_COST)
		new AbilityDefinitionAlchemistAcidBomb(DUMMY_ABIL_ID)
		..setDummyAbility()
		..setMissileArt("")
		..setMissileSpeed(0)
		..setLevels(LEVELS)
		..presetBuffs((lvl) -> BUFF_ID.toRawCode())
		..presetArmorPenalty((lvl) -> ARMOR_REDUCTION[lvl - 1])
		..presetDurationNormal((lvl) -> DURATION)
		..presetDurationHero((lvl) -> DURATION)
		..presetPrimaryDamage((lvl) -> 0)
		..presetSecondaryDamage((lvl) -> 0)
		..presetAreaofEffect((lvl) -> 0)
		..presetDamageInterval((lvl) -> 1000)
		..presetTargetsAllowed((lvl) -> TARGETS_ALLOWED)
		new BuffDefinition(BUFF_ID, 'Basl')
		..setName(1, NAME)
		..setIcon(ICON)
		..setTooltipNormal(1, NAME)
		..setTooltipNormalExtended(1, BUFF_DESC)
		..setArtTarget(1, "")

	protected static function onInit()
		AttackModifier.onHit(ID) (unit caster, unit target, int level) ->
			new DummyCaster()
			..origin(caster.getPos())
			..owner(caster.getOwner())
			..castTarget(DUMMY_ABIL_ID, level, OrderIds.acidbomb, target)
			return true


init
	CorruptedTouch.onInit()