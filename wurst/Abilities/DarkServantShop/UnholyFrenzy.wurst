package UnholyFrenzy
import Assets
import ObjectIds
import ObjectIdGenerator
import BuffObjEditing
import AbilityObjEditing
import AbilityTooltipGenerator
import HotkeyFactory
import DarkServantShop


public class UnholyFrenzy
	static constant int LEVELS = 5
	static constant int SHOP_COST = 1
	static constant HOTKEY = getHotkey()
	static constant ICON = Icons.bTNUnholyFrenzy
	static constant NAME = "Unholy Frenzy"
	static constant DESCRIPTION = "Increases the attack rate of a target unit but drains hit points each second."
	static constant BUFF_DESCRIPTION = "This unit has Unholy Frenzy; its attack rate is increased, but it takes damage over time."
	static constant BUFF_EFFECT = Abilities.unholyFrenzyTarget
	static constant real array ATTACK_SPEED_BONUS = [0.20, 0.25, 0.30, 0.35, 0.40]
	static constant real array DAMAGE_PER_SECOND = [7, 10.5,  14, 17.5, 21]
	static constant real CAST_RANGE = 500
	static constant real DURATION = 15
	static constant real COOLDOWN = 10
	static constant int array MANACOST = [50, 60, 70, 80, 90]
	static constant TARGETS_ALLOWED = "air,ground,friend,allies,self,vuln,invu,organic,neutral"

	static constant ID = compiletime(ABIL_ID_GEN.next())
	static constant BUFF_ID = compiletime(BUFF_ID_GEN.next())

	@compiletime
	private static function build()
		let tooltipGen = new AbilityTooltipGenerator(NAME, DESCRIPTION)
		..addPropertyPostfix("Attack Speed Bonus", "%")
		new AbilityDefinitionUnholyFrenzy(ID)
		..setIconNormal(ICON)
		..setHeroAbility(true)
		..setRequirements("")
		..setRace(Race.Human)
		..setName(NAME)
		..presetHotkey(HOTKEY)
		..registerTooltipGenerator(tooltipGen)
		..tooltipStartListen()
		..setLevels(LEVELS)
		..addTooltipProperty("Attack Speed Bonus", lvl -> (ATTACK_SPEED_BONUS[lvl - 1] * 100).toInt())
		..presetDamageperSecond(lvl -> DAMAGE_PER_SECOND[lvl - 1])
		..presetDurationNormal(lvl -> DURATION)
		..presetCooldown(lvl -> COOLDOWN)
		..presetCastRange(lvl -> CAST_RANGE)
		..presetManaCost(lvl -> MANACOST[lvl - 1])
		..tooltipStopListen()
		..presetAttackSpeedBonus(lvl -> ATTACK_SPEED_BONUS[lvl - 1])
		..presetDurationHero(lvl -> DURATION)
		..presetTargetsAllowed(lvl -> TARGETS_ALLOWED)
		..presetBuffs((int lvl) -> BUFF_ID.toRawCode())
		darkServantShopDef.addAbilitySold(ID, false, ICON, NAME, tooltipGen.getTooltipLearnExtended(), SHOP_COST)
		new BuffDefinition(BUFF_ID, BuffIds.unholyFrenzy)
		..setIcon(ICON)
		..setName(1, NAME)
		..setTooltipNormal(1, NAME)
		..setTooltipNormalExtended(1, BUFF_DESCRIPTION)
		..setArtTarget(1, BUFF_EFFECT)
