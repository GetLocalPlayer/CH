package TestGhoul
import Assets
import ObjectIds
import ObjectIdGenerator
import AbilityObjEditing

import ClosureEvents
import StringUtils
import ChatCommand
import TestMode


constant COMMAND_CREATE = "CreateTestGhoul"
constant SHORT_COMMAND_CREATE = "ctg"
constant COMMAND_REMOVE = "RemoveTestGhouls"
constant SHORT_COMMAND_REMOVE = "rtg"
constant COMMAND_SET_HP = "SetTestGhoulHitPoints"
constant SHORT_COMMAND_SET_HP = "stghp"
constant COMMAND_SET_ARMOR = "SetTestGhoulArmor"
constant SHORT_COMMAND_SET_ARMOR = "stga"
constant COMMAND_SET_DAMAGE = "SetTestGhoulDamage"
constant SHORT_COMMAND_SET_DAMAGE = "stgd"
constant SPAWN_RECT = gg_rct_TestGhoul
constant UNIT_ID = compiletime(UNIT_ID_GEN.next())
constant MAX_LIFE = 5000
constant ARMOR = 0.0
constant ATTACK_POWER = 10


@compiletime function build()
	new UnitDefinition(UNIT_ID, UnitIds.ghoul)
	..setAttack1DamageBase(ATTACK_POWER)
	..setAttack2DamageBase(ATTACK_POWER)
	..setHitPointsMaximumBase(MAX_LIFE)
	..setManaMaximum(5000)
	..setSpeedBase(0)
	..setAttack1DamageBase(5)
	..setDefenseBase(ARMOR.toInt())
	..setName("Test Ghoul")
	..setNormalAbilities("{0}".format(AbilityIds.neutralBuildinganyunit.toRawCode()))
	

var maxLife = MAX_LIFE
var armor = ARMOR
var attackPower = ATTACK_POWER

function initTestMode()
	let ghouls = CreateGroup()

	EventListener.add(EVENT_PLAYER_UNIT_DEATH) () ->
		let u = EventData.getDyingUnit()
		if u.getTypeId() == UNIT_ID
			ghouls.remove(u)

	var cb = ChatCommand.register(COMMAND_CREATE) (string subcommand) ->
		let list = subcommand.split(" ")
		let count = list.get(0).toInt()
		for i = 1 to count > 0 ? count : 1
			let u = createUnit(players[PLAYER_NEUTRAL_AGGRESSIVE], UNIT_ID, SPAWN_RECT.randomPoint(), GetRandomInt(0, 359).toReal().asAngleDegrees())
			..setMaxHP(maxLife)
			..setLife(maxLife.toReal())
			..setArmor(armor)
			..setBaseDamage(attackPower, 0)
			..setBaseDamage(attackPower, 1)
			ghouls.add(u)
		destroy list

	ChatCommand.register(SHORT_COMMAND_CREATE, cb)

	cb = ChatCommand.register(COMMAND_REMOVE) (string subcommand) ->
		let list = subcommand.split(" ")
		let count = list.get(0).toInt()
		for i = 1 to count.isBetween(1, ghouls.size()) ? count : ghouls.size()
			let u = ghouls.getRandom()
			ghouls.remove(u)
			u.kill()
		destroy list

	ChatCommand.register(SHORT_COMMAND_REMOVE, cb)

	cb = ChatCommand.register(COMMAND_SET_HP) (string subcommand) ->
		let list = subcommand.split(" ")
		let hp = list.get(0).toInt()
		if hp > 0
			maxLife = hp
			for u in ghouls
				u.setMaxHP(hp)
		destroy list
		
	ChatCommand.register(SHORT_COMMAND_SET_HP, cb)

	cb = ChatCommand.register(COMMAND_SET_ARMOR) (string subcommand) ->
		if subcommand != ""
			let list = subcommand.split(" ")
			armor = list.get(0).toReal()
			for u in ghouls
				u.setArmor(armor)
			destroy list
		
	ChatCommand.register(SHORT_COMMAND_SET_ARMOR, cb)

	cb = ChatCommand.register(COMMAND_SET_DAMAGE) (string subcommand) ->
		if subcommand != ""
			let list = subcommand.split(" ")
			attackPower = list.get(0).toInt()
			for u in ghouls
				u.setBaseDamage(attackPower, 0)
				u.setBaseDamage(attackPower, 1)
			destroy list

	ChatCommand.register(SHORT_COMMAND_SET_DAMAGE, cb)

init
	TestMode.onEnable() -> 
		initTestMode()