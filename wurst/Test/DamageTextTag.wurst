package DamageTextTag

import ClosureEvents
import ChatCommand

constant real ANGLE = 60
constant real VELOCITY = 0.05
constant real SIZE = 10
constant real DURATION = 1.5
constant real FADEPOINT = 0.5

constant COMMAND = "dmg"
constant SUBCOMMAND_PRE = "pre"
constant SUBCOMMAND_POST = "post"
constant SUBCOMMAND_SELF = "self"
constant SUBCOMMAND_ALL = "all"
constant SUBCOMMAND_HIDE = "hide"
constant SUBCOMMAND_SHOW = "show"


enum Mode
	PRE
	POST

enum ShowMode
	SELF
	ALL


var currMode = Mode.POST
var currShowMode = ShowMode.ALL
var show = true


init
	EventListener.add(EVENT_PLAYER_UNIT_DAMAGING) -> 
		let dmg = EventData.getEventDamage().toInt()
		if dmg > 0
			let u = EventData.getTriggerUnit()
			let source = EventData.getEventDamageSource()
			let tt = createTTEx(u.getPos3Real(), GetRandomReal(90 - ANGLE/2, 90 + ANGLE/2).asAngleDegrees().toVec(VELOCITY), dmg.toString(), SIZE, DURATION, colorA(255, 255, 255, 255))
			..setFadepoint(FADEPOINT)
			..setVisibility(false)
			if show and currMode == Mode.PRE
				if currShowMode == ShowMode.ALL or source.getOwner() == localPlayer
					tt.setVisibility(true)

	EventListener.add(EVENT_PLAYER_UNIT_DAMAGED) -> 
		let dmg = EventData.getEventDamage().toInt()
		if dmg > 0
			let u = EventData.getTriggerUnit()
			let source = EventData.getEventDamageSource()
			let tt = createTTEx(u.getPos3Real(), GetRandomReal(90 - ANGLE/2, 90 + ANGLE/2).asAngleDegrees().toVec(VELOCITY), dmg.toString(), SIZE, DURATION, colorA(255, 255, 255, 255))
			..setFadepoint(FADEPOINT)
			..setVisibility(false)
			if show and currMode == Mode.POST
				if currShowMode == ShowMode.ALL or source.getOwner() == localPlayer
					tt.setVisibility(true)
	
	ChatCommand.register(COMMAND) (string subcommand) ->
		let p = EventData.getTriggerPlayer()
		let s = subcommand.toLowerCase()
		if localPlayer == p
			if s == SUBCOMMAND_PRE
				currMode = Mode.PRE
				printTimed("Shown the damage |cFFFFCC00dealt|r", 5)
			if s == SUBCOMMAND_POST
				currMode = Mode.POST
				printTimed("Shown the damage |cFFFFCC00taken|r", 5)
			if s == SUBCOMMAND_SHOW
				show = true
				printTimed("|cFFFFCC00Damage|r is shown", 5)
			if s == SUBCOMMAND_HIDE
				show = false
				printTimed("|cFFFFCC00No damage|r is shown", 5)
			if s == SUBCOMMAND_SELF
				currShowMode = ShowMode.SELF
				printTimed("Only |cFFFFCC00your|r damage is shown", 5)
			if s == SUBCOMMAND_ALL
				currShowMode = ShowMode.ALL
				printTimed("Shown damage of |cFFFFCC00all|r players", 5)