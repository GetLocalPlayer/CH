package ItemAlias
import ItemObjEditing
import ObjectIdGenerator

import ClosureEvents
import ClosureTimers
import LinkedList
import LinkedListModule


/* An item alias is an item that copies all the fields of a specific item,
except its ability list, and with "Use Automatically When Aquired" field
set to "true". When a unit picks up an alias the trigger gives the unit
the actual item. When a unit drops an item, the item is removed and its
alias is created.
Such aliases are used to bypass the error message when a unit is trying
to pick up an item with no empty inventory slots. For example, when a hero
needs to pick up a stackable item with no empty slot but with such an item
in the inventory, or when an item's gonna be combined with other items but
it can't be done because of the annoying mechanic. */


public abstract class ItemAlias
	use LinkedListModule

	abstract function call(unit u, item givenItem)

	/** The callback will be exectued when an alias is picked up. The unit
	can have no space in the inventory for the actual item that will
	be placed on the map at the alias position. */
	static function onPickUp(ItemAlias cb) returns ItemAlias
		return cb

	protected static let itemIds = compiletime(new LinkedList<int>)
	protected static let aliasIds = compiletime(new LinkedList<int>)

	abstract static class BuildCallback
		abstract function call(ItemDefinition def, bool isAlias)

	static function build(ItemDefinition actualItemDef, ItemDefinition aliasItemDef, BuildCallback buildCb)
		buildCb.call(actualItemDef, false)
		buildCb.call(aliasItemDef, true)
		actualItemDef.setUseAutomaticallyWhenAcquired(false)
		aliasItemDef..setUseAutomaticallyWhenAcquired(true)
		..setAbilities("")
		itemIds.add(actualItemDef.getNewId())
		aliasIds.add(aliasItemDef.getNewId())
		destroy buildCb
	
	static function build(int actualItemId, int aliasItemId, BuildCallback buildCb)
		build(new ItemDefinition(actualItemId, 'afac'), new ItemDefinition(aliasItemId, 'afac'), buildCb)
	
	static function build(int actualItemId, BuildCallback buildCb)
		build(actualItemId, ITEM_ID_GEN.next(), buildCb)

	static function build(BuildCallback buildCb)
		build(ITEM_ID_GEN.next(), ITEM_ID_GEN.next(), buildCb)


init
	EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM) () ->
		let aliasItem = EventData.getManipulatedItem()
		let aliasItemId = aliasItem.getTypeId()
		if ItemAlias.aliasIds.has(aliasItemId)
			let u = EventData.getTriggerUnit()
			let pos = aliasItem.getPos()
			let p = aliasItem.getPlayer()
			let charges = aliasItem.getCharges()
			aliasItem.remove()
			let isInventoryFull = u.isInventoryFull()
			let index = ItemAlias.aliasIds.indexOf(aliasItemId)
			let actualItemId = ItemAlias.itemIds.get(index)
			let newItem = not isInventoryFull ? u.addItemById(actualItemId) : createItem(actualItemId, pos)
			newItem..setPlayer(p, true)
			..setCharges(charges)
			for cb in ItemAlias
				cb.call(u, newItem)
			if newItem.getTypeId() != 0 and isInventoryFull
				newItem.remove()
				createItem(aliasItemId, pos)
				..setPlayer(p, true)
				..setCharges(charges)


	EventListener.add(EVENT_PLAYER_UNIT_DROP_ITEM) ->
		let droppedItem = EventData.getManipulatedItem()
		let droppedItemId = droppedItem.getTypeId()
		if ItemAlias.itemIds.has(droppedItemId)
			nullTimer() () ->
				let p = droppedItem.getPlayer()
				let pos = droppedItem.getPos()
				let charges = droppedItem.getCharges()
				droppedItem.remove()
				let index = ItemAlias.itemIds.indexOf(droppedItemId)
				createItem(ItemAlias.aliasIds.get(index), pos)
				..setPlayer(p, true)
				..setCharges(charges)
